import com.commercehub.gradle.plugin.avro.GenerateAvroJavaTask

plugins {
    id 'com.commercehub.gradle.plugin.avro' version '0.99.99'
    id 'org.springframework.boot' version '2.4.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.sonarqube' version '3.1.1'
    id 'java'
}

group = 'br.com.ecommerce'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        url 'http://packages.confluent.io/maven/'
        allowInsecureProtocol(true)
    }
}

ext {
    set('springCloudVersion', "2020.0.2")
    set('swaggerVersion', "2.9.2")
    set('springBootVersion', "2.4.5")
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
    implementation 'org.springframework.data:spring-data-envers'

    // Spring Cloud
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
    implementation 'org.springframework.cloud:spring-cloud-stream'
    implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka'

    // Swagger
    implementation "io.springfox:springfox-swagger2:${swaggerVersion}"
    implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // PostgreSQL
    runtimeOnly 'org.postgresql:postgresql'

    // Kafka
    implementation 'org.apache.kafka:kafka-streams'

    // Avro
    implementation 'io.confluent:kafka-avro-serializer:5.5.0'

    // Tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

sonarqube {
    properties {
        property "sonar.projectKey", "Caiuzu_ecommerce-checkout-api"
        property "sonar.organization", "caiuzu"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}

task generateAvro(type: GenerateAvroJavaTask) {
    source("src/main/resources/avro")
    outputDir = file("src/main/java")
}